/**********************************************************************************

PROGRAM:      C:\MEPS\SAS\PROG\EXERCISE6.SAS

DESCRIPTION:  THIS PROGRAM ILLUSTRATES HOW TO POOL MEPS DATA FILES FROM DIFFERENT YEARS
              THE EXAMPLE USED IS POPULATION AGE 26-30 WHO ARE UNINSURED BUT HAVE HIGH INCOME

	         DATA FROM 2013 AND 2014 ARE POOLED.

              VARIABLES WITH YEAR-SPECIFIC NAMES MUST BE RENAMED BEFORE COMBINING FILES.  
              IN THIS PROGRAM THE INSURANCE COVERAGE VARIABLES 'INSCOV13' AND 'INSCOV14' ARE RENAMED TO 'INSCOV'.

	         SEE HC-036 (1996-2014 POOLED ESTIMATION FILE) FOR
              INSTRUCTIONS ON POOOLING AND CONSIDERATIONS FOR VARIANCE
	         ESTIMATION FOR PRE-2002 DATA.

INPUT FILE:   (1) C:\MEPS\SAS\DATA\H171.SAS7BDAT (2014 FULL-YEAR FILE)
	            (2) C:\MEPS\SAS\DATA\H163.SAS7BDAT (2013 FULL-YEAR FILE)

*********************************************************************************/;

*LIBNAME CDATA 'C:\MEPS\SAS\DATA';
LIBNAME CDATA "\\programs.ahrq.local\programs\meps\AHRQ4_CY2\B_CFACT\BJ001DVK\Workshop_2017\SAS\Data";

OPTIONS NODATE;
TITLE1 '2017 AHRQ MEPS DATA USERS WORKSHOP';
TITLE2 'EXERCISE6.SAS: POOL MEPS DATA FILES FROM DIFFERENT YEARS (2013 and 2014)';

PROC FORMAT;
	VALUE POVCAT 
    1 = '1 POOR/NEGATIVE'
    2 = '2 NEAR POOR'
    3 = '3 LOW INCOME'
    4 = '4 MIDDLE INCOME'
    5 = '5 HIGH INCOME'
    ;

	VALUE INSF
	1 = '1 ANY PRIVATE'
	2 = '2 PUBLIC ONLY'
	3 = '3 UNINSURED';

    VALUE AGE
    26-30='26-30'
    0-25='0-25'
    31-HIGH='31+';
run;

/* FREQUENCY OF 2013 */
DATA YR1;
	SET CDATA.H163 (KEEP= DUPERSID INSCOV13 PERWT13F VARSTR VARPSU POVCAT13 AGELAST TOTSLF13);
     IF PERWT13F>0;
RUN;

TITLE3 'UNWEIGHTED FREQUENCY FOR 2013 FY PERSONS WITH AGE 26-30';
PROC FREQ DATA= YR1 (WHERE=(26 LE AGELAST LE 30));
	TABLES POVCAT13*INSCOV13/ LIST MISSING ;
	FORMAT INSCOV13 INSF.  POVCAT13 POVCAT.;
RUN;

/* FREQUENCY OF 2014*/
DATA YR2;
	SET CDATA.H171 (KEEP= DUPERSID INSCOV14 PERWT14F VARSTR VARPSU POVCAT14 AGELAST TOTSLF14);
     IF PERWT14F>0;
run;

TITLE3 'UNWEIGHTED FREQUENCY FOR 2014 FY PERSONS WITH AGE 26-30';
PROC FREQ DATA= YR2 (WHERE=(26 LE AGELAST LE 30));
	TABLES POVCAT14*INSCOV14/ LIST MISSING ;
	FORMAT INSCOV14 INSF.  POVCAT14 POVCAT.;
RUN;


/* RENAME YEAR SPECIFIC VARIABLES PRIOR TO COMBINING FILES */
DATA YR1X;
	SET YR1 (RENAME=(INSCOV13=INSCOV PERWT13F=PERWT POVCAT13=POVCAT TOTSLF13=TOTSLF));
RUN;

DATA YR2X;
	SET YR2 (RENAME=(INSCOV14=INSCOV PERWT14F=PERWT POVCAT14=POVCAT TOTSLF14=TOTSLF));
RUN;

DATA POOL;
     LENGTH INSCOV AGELAST POVCAT VARSTR VARPSU 8;
	SET YR1X YR2X;
     POOLWT = PERWT/2 ;
   
     IF 26 LE AGELAST LE 30 AND POVCAT=5 AND INSCOV=3 THEN SUBPOP=1;
     ELSE SUBPOP=2;

     LABEL SUBPOP='POPULATION WITH AGE=26-30, UNINSURED WHOLE YEAR, AND HIGH INCOME'
           TOTSLF='TOTAL AMT PAID BY SELF/FAMILY';
RUN;

TITLE3 "CHECK MISSING VALUES ON THE COMBINED DATA";
PROC MEANS DATA=POOL N NMISS;
RUN;

TITLE3 'SUPPORTING CROSSTAB FOR THE CREATION OF THE SUBPOP FLAG';
PROC FREQ DATA=POOL;
	TABLES SUBPOP SUBPOP*AGELAST*POVCAT*INSCOV/ LIST MISSING ;
	FORMAT  AGELAST AGE. ;
RUN;

TITLE3 'WEIGHTED ESTIMATE ON TOTSLF FOR COMBINED DATA W/AGE=26-30, UNINSURED WHOLE YEAR, AND HIGH INCOME';
PROC SURVEYMEANS DATA=POOL NOBS MEAN STDERR;
	STRATUM VARSTR ;
	CLUSTER VARPSU ;
	WEIGHT  POOLWT;
	DOMAIN  SUBPOP;
	VAR  TOTSLF;
RUN;
