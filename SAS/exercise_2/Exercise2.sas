/*********************************************************************\

PROGRAM: 	C:\MEPS\SAS\PROG\EXERCISE2.SAS

PURPOSE:	THIS PROGRAM GENERATES SELECTED ESTIMATES FOR A 2014 VERSION OF THE
                MEPS STATISTICS BRIEF # 275: "Trends in Antipsychotics Purchases and Expenses for the U.S. Civilian
                                              Noninstitutionalized Population, 1997 and 2007"

    (1) FIGURE 1: TOTAL EXPENSE FOR ANTIPSYCHOTICS

    (2) FIGURE 2: TOTAL NUMBER OF PURCHASES OF ANTIPSYCHOTICS

    (3) FIGURE 3: TOTAL NUMBER OF PERSONS PURCHASING ONE OR MORE ANTIPSYCHOTICS

    (4) FIGURE 4: AVERAGE TOTAL, OUT OF POCKET, AND THIRD PARTY PAYER EXPENSE
                  FOR ANTIPSYCHOTICS PER PERSON WITH AN ANTIPSYCHOTIC MEDICINE PURCHASE

INPUT FILES:  (1) C:\MEPS\SAS\DATA\H1171.SAS7BDAT (2014 FULL-YEAR CONSOLIDATED PUF)
              (2) C:\MEPS\SAS\DATA\H168A.SAS7BDAT (2014 PRESCRIBED MEDICINES PUF)

\*********************************************************************/

OPTIONS LS=132 PS=79 NODATE;

*LIBNAME CDATA 'C:\MEPS\SAS\DATA';
LIBNAME CDATA "\\programs.ahrq.local\programs\meps\AHRQ4_CY2\B_CFACT\BJ001DVK\Workshop_2017\SAS\Data";

TITLE1 '2017 AHRQ MEPS DATA USERS WORKSHOP';
TITLE2 "EXERCISE2.SAS: Antipsychotics Purchases and Expenses, 2014";

PROC FORMAT;
  VALUE GTZERO
     0         = '0'
     0 <- HIGH = '>0'
     ;
RUN;

/*1) IDENTIFY ANTIPSYCHOTIC DRUGS USING THERAPEUTIC CLASSIFICATION (TC) CODES*/

DATA DRUG;
  SET CDATA.H168A;
  IF TC1=242 AND TC1S1=251; /*definition of ANTIPSYCHOTIC DRUGS*/
RUN;

TITLE3 "A SAMPLE DUMP FOR PMED RECORDS WITH ANTIPSYCHOTIC DRUGS";
PROC PRINT DATA=DRUG (OBS=30);
VAR RXRECIDX LINKIDX TC1 TC1S1 	RXXP14X RXSF14X;
 BY DUPERSID;
RUN;


/*2) SUM DATA TO PERSON-LEVEL*/

PROC SUMMARY DATA=DRUG NWAY;
  CLASS DUPERSID;
  VAR RXXP14X RXSF14X;
  OUTPUT OUT=PERDRUG (DROP=_TYPE_) sum=TOT OOP;
RUN;

TITLE3 "A SAMPLE DUMP FOR PERSON-LEVEL EXPENDITURES FOR ANTIPSYCHOTIC DRUGS";
PROC PRINT DATA=PERDRUG (OBS=30);
RUN;

DATA PERDRUG2;
 SET PERDRUG;
     RENAME _FREQ_ = N_PHRCHASE ;
     THIRD_PAYER   = TOT - OOP;
RUN;

/*3) MERGE THE PERSON-LEVEL EXPENDITURES TO THE FY PUF*/

DATA  FY;
MERGE CDATA.H171 (IN=AA KEEP=DUPERSID VARSTR VARPSU PERWT14F) 
      PERDRUG2  (IN=BB KEEP=DUPERSID N_PHRCHASE TOT OOP THIRD_PAYER);
   BY DUPERSID;

      IF AA AND BB THEN DO;
         SUB      = 1 ;
      END;

      ELSE IF NOT BB THEN DO;   /*FOR PERSONS WITHOUT ANY PURCHASE OF ANTIPSYCHOTIC DRUGS*/
         SUB         = 2 ;
         N_PHRCHASE  = 0 ;
         THIRD_PAYER = 0 ;
         TOT         = 0 ;
         OOP         = 0 ;
      END;

      IF AA;

      LABEL 
            THIRD_PAYER = 'TOTAL-OOP'
            N_PHRCHASE  = '# OF PURCHASES PER PERSON'
            SUB         = 'POPULATION FLAG FOR PERSONS WITH 1+ ANTIPSYCHOTIC DRUGS'
                        ;
RUN;

TITLE3 "SUPPORTING CROSSTABS FOR NEW VARIABLES";
PROC FREQ DATA=FY;
  TABLES  SUB * N_PHRCHASE * TOT * OOP * THIRD_PAYER / LIST MISSING ;
  FORMAT N_PHRCHASE TOT OOP THIRD_PAYER gtzero. ;
RUN;


/*4) CALCULATE ESTIMATES ON EXPENDITURES AND USE*/

ODS LISTING CLOSE;
TITLE3 "PERSON-LEVEL ESTIMATES ON EXPENDITURES AND USE FOR ANTIPSYCHOTIC DRUGS, 2014";
PROC SURVEYMEANS DATA=FY NOBS SUMWGT SUM STD MEAN STDERR;
  STRATA  VARSTR ;
  CLUSTER VARPSU;
  WEIGHT  PERWT14F;
  DOMAIN  SUB ;
  VAR TOT N_PHRCHASE  OOP THIRD_PAYER ;
  ODS OUTPUT DOMAIN=OUT1;
RUN;
ODS LISTING;

TITLE3 "RESULTS FROM PROC SURVEYMEANS WITH A DOMAIN STATEMENT";
PROC PRINT DATA=OUT1 (DROP=DOMAINLABEL) NOOBS;
FORMAT N COMMA6.0 SUMWGT SUM  STDDEV comma15.0 MEAN STDERR comma9.2  ;
RUN;

TITLE4 "SUBSET THE ESTIMATES FOR PERSONS ONLY WITH 1+ ANTIPSYCHOTIC DRUGS";
PROC PRINT DATA=OUT1 (WHERE=(SUB=1)) NOOBS;
VAR VARNAME VARLABEL N SUMWGT SUM STDDEV MEAN STDERR;
FORMAT N COMMA6.0 SUMWGT SUM  STDDEV comma15.0 MEAN STDERR comma9.2  ;
RUN;
